@Metadata.layer: #CUSTOMER
@UI: {
  headerInfo: {
    typeName: 'Stewardship Share for Cost Object',
    typeNamePlural: 'Stewardship Share for Cost Objects',
    title: {
      type: #STANDARD,
      value: 'Legalentity'
    }
  }
}
annotate view /ESRCC/C_LeCctr with
{
  @UI.facet: [ {
    id: 'CE',
    type: #COLLECTION,
    position: 10
  },
  {
    id: '/ESRCC/I_LeCctr',
    purpose: #STANDARD,
    type: #IDENTIFICATION_REFERENCE,
    parentId: 'CE',
    label: 'General Information',
    position: 10
  },
  {
    id: 'Validity',
    type: #FIELDGROUP_REFERENCE,
    parentId: 'CE',
    targetQualifier: 'ValidityGroup',
    label: 'Validity',
    position: 20
  },
  {
    id: 'Admin',
    type: #FIELDGROUP_REFERENCE,
    parentId: 'CE',
    targetQualifier: 'AdminGroup',
    label: 'Admin Information',
    position: 30
  },
  {
    id: 'ServiceParameter',
    type: #LINEITEM_REFERENCE,
    targetElement: '_ServiceParameter',
    parentId: 'CE',
    position: 40
  },
  {
     id: 'Receiver',
     purpose: #STANDARD,
     type: #LINEITEM_REFERENCE,
     parentId: 'CE',
     targetElement: '_ServiceReceiver',
     position: 50
   } ]

  @UI.identification: [ { position: 10 } ]
  @UI.lineItem: [ { position: 10 } ]
  @UI.textArrangement: #TEXT_LAST
  @Consumption.valueHelpDefinition: [{ entity: { name: '/ESRCC/I_LEGALENTITY_CCODE_F4', element: 'Sysid' },
                                       additionalBinding: [{ element: 'Legalentity', localElement: 'Legalentity' },
                                                           { element: 'Ccode', localElement: 'Ccode' }],
                                       useForValidation: true }]
  Sysid;

  @UI.lineItem: [ { position: 20 } ]
  @Consumption.valueHelpDefinition: [{ entity: { name: '/ESRCC/I_LEGALENTITY_CCODE_F4', element: 'Legalentity' },
                                       additionalBinding: [{ element: 'Ccode', localElement: 'Ccode' },
                                                           { element: 'Sysid', localElement: 'Sysid' }],
                                       useForValidation: true }]
  @UI.textArrangement: #TEXT_LAST
  Legalentity;

  @UI.identification: [ { position: 30 } ]
  @UI.lineItem: [ { position: 30 } ]
  @Consumption.valueHelpDefinition: [{ entity: { name: '/ESRCC/I_LEGALENTITY_CCODE_F4', element: 'Ccode' },
                                       additionalBinding: [{ element: 'Legalentity', localElement: 'Legalentity'},
                                                           { element: 'Sysid', localElement: 'Sysid' }],
                                       useForValidation: true }]
  @UI.textArrangement: #TEXT_LAST
  Ccode;

  @UI.identification: [ { position: 40 } ]
  @UI.lineItem: [ { position: 40, cssDefault.width: '11.5rem' } ]
  @Consumption.valueHelpDefinition: [{ entity: { name: '/ESRCC/I_COSCEN_F4', element: 'Costobject' },
                                       additionalBinding: [{ element: 'Sysid', localElement: 'Sysid', usage: #FILTER },
                                                           { element: 'Costcenter', localElement: 'Costcenter' } ],
                                       useForValidation: true }]
  @UI.textArrangement: #TEXT_LAST
  Costobject;

  @UI.identification: [ { position: 50 } ]
  @UI.lineItem: [ { position: 50 } ]
  @Consumption.valueHelpDefinition: [{ entity: { name: '/ESRCC/I_COSCEN_F4', element: 'Costcenter' },
                                       additionalBinding: [{ element: 'Sysid', localElement: 'Sysid', usage: #FILTER },
                                                           { element: 'Costobject', localElement: 'Costobject' } ],
                                       useForValidation: true }]
  @UI.textArrangement: #TEXT_LAST
  Costcenter;

  @UI.identification: [ { position: 90 } ]
  @UI.lineItem: [ { position: 90 } ]
  @Consumption.valueHelpDefinition: [{ entity: { name: '/ESRCC/I_PROFITCENTER_F4', element: 'ProfitCenter' }, useForValidation: true }]
  @UI.textArrangement: #TEXT_LAST
  Profitcenter;

  @UI.identification: [ { position: 100 } ]
  @UI.lineItem: [ { position: 100 } ]
  @Consumption.valueHelpDefinition: [{ entity: { name: '/ESRCC/I_BUSINESSDIV_F4', element: 'BusinessDivision' }, useForValidation: true }]
  @UI.textArrangement: #TEXT_LAST
  Businessdivision;

  @UI.identification: [ { position: 80 } ]
  @UI.lineItem: [ { position: 80, cssDefault.width: '12.5rem' } ]
  Stewardship;

  @UI.lineItem: [ { position: 60 } ]
  @UI.fieldGroup: [{ qualifier: 'ValidityGroup', position: 10 }]
  Validfrom;

  @UI.lineItem: [ { position: 70 } ]
  @UI.fieldGroup: [{ qualifier: 'ValidityGroup', position: 20 }]
  Validto;

  @UI.lineItem: [ { position: 130 } ]
  @UI.fieldGroup: [{ qualifier: 'AdminGroup', position: 10 }]
  CreatedBy;

  @UI.lineItem: [ { position: 140, cssDefault.width: '9.5rem' } ]
  @UI.fieldGroup: [{ qualifier: 'AdminGroup', position: 20 }]
  CreatedAt;

  @UI.lineItem: [ { position: 150 } ]
  @UI.fieldGroup: [{ qualifier: 'AdminGroup', position: 30 }]
  LastChangedBy;

  @UI.lineItem: [ { position: 160, cssDefault.width: '9.5rem' } ]
  @UI.fieldGroup: [{ qualifier: 'AdminGroup', position: 40 }]
  LastChangedAt;

  @UI.hidden: true
  SysidDesc;

  @UI.hidden: true
  LegalEntityDesc;

  @UI.hidden: true
  CcodeDesc;

  @UI.hidden: true
  CostobjectDesc;

  @UI.hidden: true
  CostcenterDesc;

  @UI.hidden: true
  ProfitcenterDesc;

  @UI.hidden: true
  BusinessdivisionDesc;
}