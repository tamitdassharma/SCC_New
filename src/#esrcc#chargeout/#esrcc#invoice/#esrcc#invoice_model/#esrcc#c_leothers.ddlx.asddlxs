@Metadata.layer: #CUSTOMER
@UI: {
  headerInfo: {
    typeName: 'Additional Information',
    typeNamePlural: 'Additional Information',
    title: {
      type: #STANDARD,
      label: 'Additional Information',
      value: 'LegalEntity'
    }
  }
}
annotate view /ESRCC/C_LeOthers with
{
  @UI.facet: [ {
    id: 'CE',
    type: #COLLECTION,
    purpose: #STANDARD,
    position: 1
  },
  {
    id: '/ESRCC/I_LeOthers',
    purpose: #STANDARD,
    type: #IDENTIFICATION_REFERENCE,
    parentId: 'CE',
    label: 'General Information',
    position: 1
  },
  {
    id: 'Admin',
    type: #FIELDGROUP_REFERENCE,
    parentId: 'CE',
    targetQualifier: 'AdminGroup',
    label: 'Admin Information',
    position: 2
  } ]

  @UI.identification: [ { position: 1, hidden: true } ]
  @UI.lineItem: [ { position: 1 } ]
  @UI.textArrangement: #TEXT_LAST
  @Consumption.valueHelpDefinition: [{ entity: { name: '/ESRCC/I_COMPANYCODES_F4', element: 'Legalentity' },
                                       additionalBinding: [{ localElement: 'Sysid', element: 'Sysid' },
                                                           { localElement: 'CompanyCode', element: 'Ccode' }],
                                       useForValidation: true }]
  LegalEntity;

  @UI.identification: [ { position: 2 } ]
  @UI.lineItem: [ { position: 2, cssDefault.width: '13rem' } ]
  @UI.textArrangement: #TEXT_LAST
  @Consumption.valueHelpDefinition: [{ entity: { name: '/ESRCC/I_ROLE', element: 'Role' },
                                       useForValidation: true }]
  Role;

  @UI.identification: [ { position: 3 } ]
  @UI.lineItem: [ { position: 3 } ]
  @UI.textArrangement: #TEXT_LAST
  @Consumption.valueHelpDefinition: [{ entity: { name: '/ESRCC/I_COMPANYCODES_F4', element: 'Sysid' },
                                       additionalBinding: [{ localElement: 'LegalEntity', element: 'Legalentity' },
                                                           { localElement: 'CompanyCode', element: 'Ccode' }],
                                       useForValidation: true }]
  Sysid;

  @UI.identification: [ { position: 4 } ]
  @UI.lineItem: [ { position: 4 } ]
  @UI.textArrangement: #TEXT_LAST
  @Consumption.valueHelpDefinition: [{ entity: { name: '/ESRCC/I_COMPANYCODES_F4', element: 'Ccode' },
                                       additionalBinding: [{ localElement: 'Sysid', element: 'Sysid' },
                                                           { localElement: 'LegalEntity', element: 'Legalentity' }],
                                       useForValidation: true }]
  CompanyCode;

  @UI.identification: [ { position: 5 } ]
  @UI.lineItem: [ { position: 5, cssDefault.width: '11.5rem' } ]
  @UI.textArrangement: #TEXT_LAST
  @Consumption.valueHelpDefinition: [{ entity: { name: '/ESRCC/I_COSTOBJECTS', element: 'Costobject' } }]
  Costobject;

  @UI.identification: [ { position: 6 } ]
  @UI.lineItem: [ { position: 6 } ]
  @UI.textArrangement: #TEXT_LAST
  @Consumption.valueHelpDefinition: [{ entity: { name: '/ESRCC/I_BUSINESSDIV_F4', element: 'BusinessDivision' },
                                       useForValidation: true }]
  BusinessDivision;

  @UI.identification: [ { position: 7 } ]
  @UI.lineItem: [ { position: 7 } ]
  @UI.textArrangement: #TEXT_LAST
  @Consumption.valueHelpDefinition: [{ entity: { name: '/ESRCC/I_TRANSACTIONGROUP_F4', element: 'Transactiongroup' },
                                       useForValidation: true }]
  TransactionGroup;

  @UI.identification: [ { position: 8 } ]
  @UI.lineItem: [ { position: 8 } ]
  @EndUserText.label: 'Account'
  Account;

  @UI.identification: [ { position: 9 } ]
  @UI.lineItem: [ { position: 9 } ]
  @EndUserText.label: 'Business Partner Number'
  BusinessPartnerNumber;

  @UI.lineItem: [ { position: 10 } ]
  @UI.fieldGroup: [{ qualifier: 'AdminGroup', position: 1 }]
  CreatedBy;

  @UI.lineItem: [ { position: 11, cssDefault.width: '9.5rem' } ]
  @UI.fieldGroup: [{ qualifier: 'AdminGroup', position: 2 }]
  CreatedAt;

  @UI.lineItem: [ { position: 12 } ]
  @UI.fieldGroup: [{ qualifier: 'AdminGroup', position: 3 }]
  LastChangedBy;

  @UI.lineItem: [ { position: 13, cssDefault.width: '9.5rem' } ]
  @UI.fieldGroup: [{ qualifier: 'AdminGroup', position: 4 }]
  LastChangedAt;

  @UI.hidden: true
  LegalentityDescription;
  @UI.hidden: true
  CcodeDescription;
  @UI.hidden: true
  RoleDescription;
  @UI.hidden: true
  SysidDescription;
  @UI.hidden: true
  CostObjectDescription;
  @UI.hidden: true
  BusinessDivisionDescription;
  @UI.hidden: true
  TransactionGroupDescription;
}